() send_grams(slice address, int amount) impure {
  cell msg = begin_cell()
    .store_uint (0x18, 6) ;; bounce
    .store_slice(address) ;; 267 bit address
    .store_grams(amount)
    .store_uint(0, 107) ;; 106 zeroes +  0 as an indicator that there is no cell with the data
    .end_cell(); 
  send_raw_message(msg, 3); ;; mode, 2 for ignoring errors, 1 for sender pays fees, 64 for returning inbound message value
}

cell pack_oracle_client_data(slice owner_address, slice oracle_master_address, slice sc_address, int comission_size, cell whitelisted_oracle_addresses, int mode, int interval) inline {
  return  begin_cell()
            .store_uint(0, 64)
            .store_ref(begin_cell().store_slice(owner_address).store_slice(oracle_master_address).store_slice(sc_address).end_cell())
            .store_uint(comission_size, 64)
            .store_ref(whitelisted_oracle_addresses)
            .store_uint(mode, 32)
            .store_uint(interval, 32)
           .end_cell();
}

cell calculate_oracle_client_state_init(slice owner_address, slice oracle_master_address, slice sc_address, cell oracle_client_code, int comission_size, cell whitelisted_oracle_addresses) inline {
  
  int default_mode = mode::on_demand();

  return begin_cell()
          .store_uint(0, 6)
          .store_dict(oracle_client_code)
          .store_dict(pack_oracle_client_data(owner_address, oracle_master_address, sc_address, comission_size, whitelisted_oracle_addresses, default_mode, 0))
          .store_uint(0, 1)
         .end_cell();
}

slice calculate_oracle_client_address(cell state_init) inline {
  return begin_cell().store_uint(4, 3)
                     .store_int(workchain(), 8)
                     .store_uint(cell_hash(state_init), 256)
                     .end_cell()
                     .begin_parse();
}